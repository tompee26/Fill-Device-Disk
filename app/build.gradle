apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        applicationId "com.tompee.utilities.filldevicespace"

        versionCode 9
        versionName "3.1.000"
        multiDexEnabled true
    }

    signingConfigs {
        if (project.hasProperty("release.properties") &&
                new File(project.property("release.properties")).exists()) {
            Properties props = new Properties()
            props.load(new FileInputStream(file(project.property("release.properties"))))

            release {
                storeFile file(props["keystore"])
                storePassword props['keystore.password']
                keyAlias props['keystore.alias']
                keyPassword props['keystore.aliaspassword']
            }
        }
    }

    buildTypes {
        if (project.hasProperty("release.properties") &&
                new File(project.property("release.properties")).exists()) {
            release {
                shrinkResources true
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding = true
    }

    aaptOptions {
        noCompress "dat"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "androidx.core:core-ktx:$core"
    implementation "androidx.fragment:fragment-ktx:$fragment"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle"
    implementation "androidx.multidex:multidex:$multidex"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayout"

    implementation "androidx.viewpager2:viewpager2:$viewPager"
    implementation "com.google.android.material:material:$support"

    implementation "com.google.dagger:dagger:$dagger"
    implementation "com.google.dagger:dagger-android:$dagger"
    implementation "com.google.dagger:dagger-android-support:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"

    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroid"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlin"
    implementation "com.jakewharton.rx2:replaying-share-kotlin:$replaying_share"
    implementation "com.jakewharton.timber:timber:$timber"

    implementation "com.google.firebase:firebase-core:$firebaseCore"
    implementation "com.google.firebase:firebase-ads:$firebaseAds"

    def easyPermission = "1.3.0"
    implementation "pub.devrel:easypermissions:$easyPermission"

    def mpAndroidChart = "v3.0.0-beta1"
    implementation "com.github.PhilJay:MPAndroidChart:$mpAndroidChart"

    def circleProgressView = "v1.2.9.1"
    implementation "com.github.jakob-grabner:Circle-Progress-View:$circleProgressView"
}

apply plugin: 'com.google.gms.google-services'